buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:2.2.0'
        classpath 'pl.allegro.tech.build:axion-release-plugin:1.1.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'optional-base'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'pl.allegro.tech.build.axion-release'

sourceCompatibility = 1.7
targetCompatibility = 1.7

scmVersion { tag { prefix = project.name } }
project.version = scmVersion.version
project.group = 'pl.patrykpacewicz'

repositories {
    mavenCentral()
}

dependencies {
    compile 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:1.46.4'
    compile 'junit:junit:4.12', optional

    testCompile 'org.mongodb:mongo-java-driver:2.13.0'
    testCompile 'org.assertj:assertj-core:1.7.1'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.0'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar { classifier "sources" }
        }
    }
}

bintray {
    user = project.properties['bintrayUser']   ? bintrayUser   : ''
    key  = project.properties['bintrayApiKey'] ? bintrayApiKey : ''
    publications = ['mavenJava']
    pkg {
        name = project.name
        repo = 'maven'
        version { name = project.version }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
